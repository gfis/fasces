10 REM 30.11.85 MARIO HILGEMEIER
20 REM   INTEGRATION/REDUKTION ANALYSE/SYNTHESE GLEICHNISZAHLEN-REIHE
22 REM--------------------------------------------------------------------
25 LET LENG=8000
30 DIM R$(LENG),N$(LENG)
40 LET N$="1"
50 LET NUMMER=1
98 REM
99 REM SCHLEIFE 1
100 LET R$=N$
101 LET N$=""
105 INPUT "Return "; DUMMY$
110 PRINT : PRINT "Kette Nummer ";NUMMER: PRINT R$
115 LET NUMMER=NUMMER+1
120 LET ENDE=LEN(R$)
125 LET ENDFLAG=0
130 LET ZIFFZEHLER=1: LET POSITION=1
135 LET ZIFF1$=MID$(R$,POSITION,1)
136 REM
137 REM SCHLEIFE 2
145 LET POSITION=POSITION+1
150 IF ENDE<POSITION THEN LET ENDFLAG=1: GOTO 400
154 REM
155 REM NÄCHSTE ZIFFER = LETZTE  ZIFFER ?
160 LET ZIFF2$=MID$(R$,POSITION,1)
170 IF ZIFF1$=ZIFF2$ THEN LET ZIFFZEHLER=ZIFFZEHLER+1: GOTO 145
398 REM
399 REM NEUSTRING VERLÄNGERN
400 LET H$=RIGHT$(STR$(ZIFFZEHLER),1)+ZIFF1$: LET N$=N$+H$
410 IF ENDFLAG=1 THEN LET ENDFLAG=0: GOTO 98
420 LET ZIFFZEHLER=1: LET ZIFF1$=ZIFF2$
430 GOTO 145
998 STOP
999 DISC SAVE "REDUKT"
1000 RUN